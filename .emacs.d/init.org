#+TITLE: DDav Emacs configuration
#+PROPERTY: header-args:emacs-lisp :tangle ~/.emacs.d/init.el :mkdirp yes

* Bootstrap

On *first install* execute org-babel-tangle (C-c C-v t) to generate =init.el= inside user directory.

Add hook to automatically tangle of config file on save.

#+begin_src emacs-lisp
  (defun ddav/org-babel-tangle-config ()
    (when (string-equal (f-filename buffer-file-name) "init.org")
      (org-babel-tangle)))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'ddav/org-babel-tangle-config)))
#+end_src

Define reload config function for easy testing of changes in =init.org=

#+begin_src emacs-lisp
  (defun ddav/reload-config ()
    "Reload config file"
    (interactive)
    (load user-init-file))
#+end_src

* Package manager setup

Setup repositories and install =use-package=

#+begin_src emacs-lisp
  (require 'package)

  (setq package-archives
        '(("melpa" . "https://raw.githubusercontent.com/d12frosted/elpa-mirror/master/melpa/")
          ("org"   . "https://raw.githubusercontent.com/d12frosted/elpa-mirror/master/org/")
          ("gnu"   . "https://raw.githubusercontent.com/d12frosted/elpa-mirror/master/gnu/")))

  (package-initialize)

  (unless package-archive-contents (package-refresh-contents))

  ;; Install package manager if not present
  (unless (package-installed-p 'use-package)
    (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure t)
#+end_src

Add auto update package

#+begin_src emacs-lisp
  (use-package auto-package-update)
#+end_src

* Emacs general config

Use =no-littering= package to keep emacs folder clean

#+begin_src emacs-lisp
;;  (use-package no-littering)

;;  (setq auto-save-file-name-transforms
;;        '((".*" '(no-littering-expand-var-file-name "auto-save/") t)))
#+end_src

Lets disable autosave, because it is annoying having autosave files generated everywhere.

#+begin_src emacs-lisp
  (setq auto-save-default nil)
#+end_src

Have backup in single directory. Again annoying to have backup files scattered in work folders.

#+begin_src emacs-lisp
  (setq backup-directory-alist '(("" . "~/.emacs.d/backups")))
#+end_src

Increase GC collector memory size, to reduce number of collections during emacs startup and usage:

#+begin_src emacs-lisp
  (setq gc-const-threshold (* 2 1000 1000)) ; 2 MB
#+end_src

* Enable evil mode

#+begin_src emacs-lisp
  (defun ddav/evil-hook ()
    (dolist (mode '(custom-mode
                    eshell-mode
                    git-rebase-mode
                    erc-mode
                    term-mode))
      (add-to-list 'evil-emacs-state-modes mode)))

  (use-package evil
    :init
    (setq evil-want-integration t)
    (setq evil-want-keybinding nil)
    (setq evil-want-C-u-srcoll t)
    (setq evil-want-C-i-jump nil)
    (evil-mode 1)
    :hook (evil-mode . ddav/evil-hook)
    :config
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)
    (evil-set-initial-state 'messages-bgger-mode 'normal)
    (evil-set-initial-state 'dashboard-mode 'normal))

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))
#+end_src

* UI Configuration

Remove all menus and bars, for a minimalistic look.

#+begin_src emacs-lisp
  (setq inhibit-startup-message t)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
  (tooltip-mode -1)
#+end_src

Spacing between panels is defined with:

#+begin_src emacs-lisp
  (set-fringe-mode 10)
#+end_src

** Line numbers

Enable relative line numbers only for prog modes through hook.

#+begin_src emacs-lisp
  (column-number-mode)
  (setq display-line-numbers-type 'relative)

  (defun ddav/prog-mode-hook ()
    (display-line-numbers-mode t))

  (add-hook 'prog-mode-hook #'ddav/prog-mode-hook)
#+end_src

** Font

Font must be set before theme.

#+begin_src emacs-lisp
  (set-face-attribute 'default nil :font "FiraCode NF")

  (set-face-attribute 'fixed-pitch nil :font "FiraCode NF")

  (set-face-attribute 'variable-pitch nil :font "Source Sans Pro" :weight 'regular :height 120)
#+end_src

** Theme

Borrow theme and modeline from Doom since they work well together

#+begin_src emacs-lisp
  (use-package doom-themes
    :config
    (load-theme 'doom-tomorrow-night t)
    (doom-themes-visual-bell-config)
    (doom-themes-org-config))

  (use-package doom-modeline
    :init (doom-modeline-mode 1)
    :custom (doom-modeline-height 40))
#+end_src

All the icons is required by doom theme and modeline

#+begin_src emacs-lisp
  (use-package all-the-icons)
#+end_src

Colored parens

#+begin_src emacs-lisp
  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))
#+end_src

** Tree view

Add tree view powered by treemacs package https://github.com/Alexander-Miller/treemacs

#+begin_src emacs-lisp
  (use-package treemacs
    :defer t
    :config
    (progn
      (setq treemacs-user-mode-line-format 'none)))

  (use-package treemacs-evil
    :after (treemacs evil))

  (use-package treemacs-projectile
    :after (treemacs projectile))

  (use-package treemacs-magit
    :after (treemacs magit))
#+end_src

* Keybindings

Define global keybinding to remap exit function to =ESC=

#+begin_src emacs-lisp
  (global-set-key (kbd "<escape>") 'keyboard-quit)
#+end_src

** General

General helps defining custom bindings prefixed by =SPC= leader, like spacemacs.

TODO: lookup spacemacs or doom emacs bindings for some inspiration

#+begin_src emacs-lisp
  (use-package general
    :after evil
    :config
    (general-evil-setup t)
    (general-create-definer ddav/leader-keys
      :keymaps '(normal visual emacs)
      :prefix "SPC"
      :global-prefix "C-SPC")

    (ddav/leader-keys
      "a" '(org-agenda :which-key "agenda")
      "b" '(nil :which-key "buffer")
      "b b" '(consult-buffer :which-key "list buffers")
      "b l" '(list-buffers :which-key "list buffers")
      "b k" '(kill-buffer :which-key "kill buffer")
      "d" '(dired :which-key "dired")
      "g" '(magit-status :which-key "magit")
      "h" '(nil :which-key "help")
      "h f" '(helpful-callable :which-key "function help")
      "h k" '(helpful-callable :which-key "keybind help")
      "h v" '(helpful-callable :which-key "variable help")
      "h r" '(ddav/reload-config :which-key "reload configuration")
      "h u" '(auto-package-update-now :which-key "update packages")
      "n" '(nil :which-key "org-roam")
      "n d" '(org-roam-dailies-map :which-key "dailies")
      "n f" '(org-roam-node-find :which-key "node find")
      "n i" '(org-roam-node-insert :which-key "node insert")
      "n l" '(org-roam-buffer-toggle :which-key "backlinks")
      "p" '(projectile-command-map :which-key "projectile")
      "t" '(treemacs :which-key "toggle tree")))
      #+end_src

** Which key

Which key gives description for keybindings, very useful for documenting keybindings

#+begin_src emacs-lisp
  (use-package which-key
    :init (which-key-mode)
    :diminish
    :config (setq which-key-idle-delay 0.3))
#+end_src

* Completion framework

Build completion framework from modular packages

** Vertico

Minimalistic completion UI based on default completion system.

https://github.com/minad/vertico

#+begin_src emacs-lisp
  (use-package vertico
    :init (vertico-mode))
#+end_src

Also save history

#+begin_src emacs-lisp
  (use-package savehist
    :init (savehist-mode))
#+end_src

** Marginalia

Gives description for commands, directories, etc in minibuffer

https://github.com/minad/marginalia

#+begin_src emacs-lisp
  (use-package marginalia
    :init (marginalia-mode))
#+end_src

** Consult

Enhanced completion

https://github.com/minad/consult

#+begin_src emacs-lisp
  (use-package consult
    :init 
    :bind
    ("C-x b" . consult-buffer)) ; replaces switch-to-buffer
#+end_src

** Orderless

https://github.com/oantolin/orderless

Orderless provides unordered completion, i.e. you dont have to type stuff in the exact order
to find it. This is very useful for searching commands, but more importantly for projectile
file finder.

#+begin_src emacs-lisp
  (use-package orderless
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles basic partial-completion)))))
#+end_src

** Corfu

Enhance completion at point with corfu package https://github.com/minad/corfu

#+begin_src emacs-lisp
  (use-package corfu
    :init
    (global-corfu-mode)
    :custom
    (corfu-auto t)
    (corfu-quit-no-match 'separator))
#+end_src

* Helpers
** Helpful

https://github.com/Wilfred/helpful

Enhanced help buffer remap help bindings to use helpful


#+begin_src emacs-lisp
  (use-package helpful
    :bind
    (([remap describe-function] . helpful-callable)
     ([remap describe-command] . helpful-command)
     ([remap describe-variable] . helpful-variable)
     ([remap describe-key] . helpful-key)))
#+end_src

** Hydra

https://github.com/abo-abo/hydra

Helper package that allows define repeatable functions

#+begin_src emacs-lisp
  (use-package hydra)
#+end_src

Here is an helper function to resize an open window split:

#+begin_src emacs-lisp
  (defhydra hydra-window-resize (:timeout 4)
    "resize window split"
    ("j" evil-window-increase-height "height++")
    ("k" evil-window-decrease-height "height--")
    ("h" evil-window-decrease-width "width--")
    ("l" evil-window-increase-width "width++")
    ("f" nil "finish" :exit t))
#+end_src

** Projectile

https://github.com/bbatsov/projectile

Projectile helps handling projects and searching files within a project

#+begin_src emacs-lisp
  (use-package projectile
    :diminish
    :config
    (projectile-mode)
    :bind
    ("C-c p" . 'projectile-command-map)
    :init
    (setq projectile-switch-project-action #'projectile-dired))
#+end_src

** Dashboard

https://github.com/emacs-dashboard/emacs-dashboard

Provide dashboard to navigate recent files and projects. Works great in combination with projectile.

#+begin_src emacs-lisp
  (use-package dashboard
    :config
    (setq dashboard-set-heading-icons t)
    (setq dashboard-set-file-icons t)
    (setq dashboard-project-backend 'projectile)
    (dashboard-setup-startup-hook))
#+end_src

* Programming
** Eglot

Eglot lsp client setup.

https://github.com/joaotavora/eglot

TODO: this can e removed when emacs 29 releases, since eglot will be incuded

#+begin_src emacs-lisp
  (use-package eglot
    :hook
    ((c++-mode . eglot-ensure)
     (rust-mode . eglot-ensure))
    :config
    (add-to-list 'eglot-server-programs
                 '(rust-mode . ("rust-analyzer"))
                 '(c++-mode . ("clangd"))))
#+end_src

** Flycheck

Replace builtin flymake with flycheck errors reporter, which is more customizable and shows
automatically errors info.

#+begin_src emacs-lisp
  (use-package flycheck
    :init (global-flycheck-mode))
#+end_src

** Magit

Setup magit. Only customization here is to display diff in a different buffer.

#+begin_src emacs-lisp
  (use-package magit
    :commands magit-status
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))
#+end_src
** Yaml

Add yaml support through =yaml-mode= package.

#+begin_src emacs-lisp
  (use-package yaml-mode
    :config
    (add-to-list 'auto-mode-alist '("\\.yaml\\'" . yaml-mode)))
#+end_src

** Cmake

Add cmake support

#+begin_src emacs-lisp
  (use-package cmake-mode
    :config
    (add-to-list 'auto-mode-alist '("CMakeLists\\.txt\\'" . cmake-mode))
    (add-to-list 'auto-mode-alist '("\\.cmake\\'" . cmake-mode)))
#+end_src

* Org mode

Let's customize org mode:

Here we define the hook to enable some configs when =org-mode= starts up.

=org-indent-mode= enables indentation of headings.

=visual-line-mode= enables word wrap.

=variable-pitch-mode= enables variable pitch font for =org-mode=. For exclusions see [[Font]] section.

#+begin_src emacs-lisp
  (defun ddav/org-mode-setup ()
    (org-indent-mode)
    (visual-line-mode 1)
    (auto-fill-mode 0)
    (variable-pitch-mode 1))
#+end_src

Main =org-mode= configuration through =use-package=

#+begin_src emacs-lisp
  (use-package org
    :hook (org-mode . ddav/org-mode-setup)
    :config
    (dolist (face '((org-level-1 . 1.4)
                    (org-level-2 . 1.2)
                    (org-level-3 . 1.1)
                    (org-level-4 . 1.05)
                    (org-level-5 . 1.0)
                    (org-level-6 . 1.0)
                    (org-level-7 . 1.0)
                    (org-level-8 . 1.0)))
      (set-face-attribute (car face) nil :font "Source Sans Pro" :weight 'regular :height (cdr face)))


    (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-code nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-table nil :inherit '(shadow fixed-pitch))
                                          ; (set-face-attribute 'org-indent nil :inherit '(org-hide fixed-pitch))
    (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch)

    ;(setq org-agenda-files
    ;      '("~/org/journal"
    ;        "~/org/zk"))

    (setq org-todo-keywords
          '((sequence "TODO(t)" "NEXT(n)" "IN PROGRESS(p)" "|" "DONE(d!)")))

    ;; Use "agenda" tag to mark org-roam files to be parsed bt agenda. For this
    ;; reason we don't want the "agenda" tag to be inherited from all todo items.
    (add-to-list 'org-tags-exclude-from-inheritance "agenda")
    :custom
    (org-ellipsis " â—‚")
    (org-hide-emphasis-markers t))
#+end_src

Add some automation to have org-roam work nicely with org-agenda:

Taken from https://d12frosted.io/posts/2021-01-16-task-management-with-roam-vol5.html

#+begin_src emacs-lisp
  (use-package vulpea
    :init)

  (defun ddav/org-roam-node-has-todo ()
    "Return non-nil if current buffer has a TODO entry"
    (org-element-map
     (org-element-parse-buffer 'headline)
     'headline
     (lambda (h)
       (eq (org-element-property :todo-type h)
           'todo))
     nil 'first-match))

  (defun ddav/buffer-is-a-note ()
      "Return non nil if the current buffer is a note."
    (and buffer-file-name
         (string-prefix-p
          (expand-file-name (file-name-as-directory org-roam-directory))
          (file-name-directory buffer-file-name))))

  (defun ddav/org-roam-node-update-agenda-tag ()
      "Update agenda tag in current buffer."
    (when (and (not (active-minibuffer-window))
               (ddav/buffer-is-a-note))
      (save-excursion
        (goto-char (point-min))
        (let* ((tags (vulpea-buffer-tags-get))
               (original-tags tags))
          (if (ddav/org-roam-node-has-todo)
              (setq tags (cons "agenda" tags))
            (setq tags (remove "agenda" tags)))

          ;; cleanup duplicates
          (setq tags (seq-uniq tags))

          ;; update tags if changed
          (when (or (seq-difference tags original-tags)
                    (seq-difference original-tags tags))
            (apply #'vulpea-buffer-tags-set tags))))))

  (add-hook 'find-file-hook #'ddav/org-roam-node-update-agenda-tag)
  (add-hook 'before-save-hook #'ddav/org-roam-node-update-agenda-tag)

  (defun ddav/org-agenda-query-files ()
    "Return a list of org-roam nodes containing the 'agenda' tag."
    (message "Query agenda files")
    (seq-uniq
     (seq-map
      #'car
      (org-roam-db-query
       [:select [nodes:file]
        :from tags
        :left-join nodes
        :on (= tags:node-id nodes:id)
        :where (like tag (quote "%\"agenda\"%"))]))))


  (defun ddav/org-agenda-files-update (&rest _)
    "Update value og org-agenda-files"
    (setq org-agenda-files (ddav/org-agenda-query-files)))

  (advice-add 'org-agenda :before #'ddav/org-agenda-files-update)
  (advice-add 'org-todo-list :before #'ddav/org-agenda-files-update)
#+end_src

Display note title in agenda instead of filename

#+begin_src emacs-lisp
  (defun ddav/org-agenda-category ()
    "Get category of item at point for agenda."

    (let* ((file-name (when buffer-file-name
                        (file-name-sans-extension
                         (file-name-nondirectory buffer-file-name))))
           (title (vulpea-buffer-prop-get "title"))
           (category (org-get-category)))
      (or (if (and
               title
               (string-equal category file-name))
              title
            category)
          "")))

  (setq org-agenda-prefix-format
        '((agenda . " %i %-12(ddav/org-agenda-category)%?-12t% s")
          (todo . " %i %-12(ddav/org-agenda-category) ")
          (tags . " %i %-12(ddav/org-agenda-category) ")
          (search . " %i %-12(ddav/org-agenda-category) ")))
#+end_src

** Org appear

Hide markers in org mode and visualize them only when in evil edit mode:

#+begin_src emacs-lisp
  (use-package org-appear
    :after org
    :hook (org-mode . org-appear-mode)
    :custom
    (org-appear-trigger 'always)
    (org-appear-autolinks t))
#+end_src

** Visual fill mode

This package adds padding on the left and right of buffers in =org-mode=. This makes org mode look more like a document editor.

#+begin_src emacs-lisp
  (defun ddav/org-mode-visual-fill ()
    (setq visual-fill-column-width 120
          visual-fill-column-center-text t)
    (visual-fill-column-mode 1))

  (use-package visual-fill-column
    :defer t
    :hook (org-mode . ddav/org-mode-visual-fill))
#+end_src

**  Org roam

Org roam is a package that implements Zettlekasten note taking method.

#+begin_src emacs-lisp
  (use-package org-roam
    :init
    (require 'org-roam-dailies) ; Ensure dailies keymap is available
    :bind
    (("C-c n l" . org-roam-buffer-toggle)
    ("C-c n f" . org-roam-node-find)
    ("C-c n i" . org-roam-node-insert))
    :bind-keymap
    ("C-c n d". org-roam-dailies-map)
    :config
    (org-roam-setup)
    (org-roam-db-autosync-mode)
    :custom
    (org-roam-directory "~/org/zk")
    (org-roam-dailies-directory "../journal") ; relative to org-roam-directory

    ;; Display tags in org-roam-node-find
    (org-roam-node-display-template (concat "${title} "
                                            (propertize "${tags:200}" 'face 'org-tag)))
    (org-roam-capture-templates
     '(("d" "default" plain
        "%?"
        :if-new (file+head "%<%Y%m%d%h%m%S>-${slug}.org" "#+title: ${title}\n")
        :unnarrowed t)
       ("b" "BCP21" plain
        "%?"
        :if-new (file+head "%<%Y%m%d%h%m%S>-${slug}.org" "#+title: ${title}\n#+filetags: :@work:bcp21:\n")
        :unnarrowed t)))

    (org-roam-dailies-capture-templates
     '(("d" "default" entry
        "* TODO %?"
        :target (file+head "%<%Y-%m-%d>.org" "#+title: %<%Y-%m-%d>\n#+filetags :agenda:\n")))))
#+end_src

* File management

Configure and enhance =dired=

#+begin_src emacs-lisp
  (use-package dired
    :ensure nil ; make sure use-package does not attempt to install
    :commands (dired dired-jump)
    :bind (("C-x C-j". dired-jump))
    :config

    (evil-collection-define-key 'normal 'dired-mode-map
      "h" 'dired-single-up-directory
      "l" 'dired-single-buffer)
    :custom
    (dired-listing-switches "-agho --group-directories-first")
    (delete-by-moving-to-trash t))

  (use-package dired-single) ; single buffer for dired

  (use-package all-the-icons-dired
    :hook (dired-mode . all-the-icons-dired-mode))
#+end_src
